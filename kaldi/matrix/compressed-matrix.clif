from "matrix/matrix-common-clifwrap.h" import *
from "matrix/kaldi-vector-clifwrap.h" import *
from "matrix/kaldi-matrix-clifwrap.h" import *

from "matrix/compressed-matrix.h":
  namespace `kaldi`:
    enum CompressionMethod with:
      `kAutomaticMethod` as AUTO
      `kSpeechFeature` as SPEECH_FEATURE
      `kTwoByteAuto` as TWO_BYTE_AUTO
      `kTwoByteSignedInteger` as TWO_BYTE_SIGNED_INTEGER
      `kOneByteAuto` as ONE_BYTE_AUTO
      `kOneByteUnsignedInteger` as ONE_BYTE_UNSIGNED_INTEGER
      `kOneByteZeroOne` as TWO_BYTE_ZERO_ONE

    class CompressedMatrix:
      @add__init__
      def `CompressedMatrix` as new(self, mat: MatrixBase,
                                    method: CompressionMethod = default)

      @add__init__
      def `CompressedMatrix` as new_with_range(self, mat: CompressedMatrix,
                                             row_offset: int, num_rows: int,
                                             col_offset: int, num_cols:int,
                                             allow_padding: bool = default)

      @add__init__
      def `CompressedMatrix` as new_with_other(self, mat: CompressedMatrix)

      def `CopyFromMat` as copy_from_mat(self, mat: MatrixBase,
                      method: CompressionMethod = default):
        """Copy data from another matrix to self.

        Args:
            mat(MatrixBase): The matrix from which we will copy the data
            method(CompresssionMethod): Which compression method to use
        """ 
      
      def `CopyToMat` as copy_to_mat(self, mat: MatrixBase,
                    method: MatrixTransposeType = default)

      def `NumRows` as num_rows(self) -> int:
        """Returns the number of rows."""

      def `NumCols` as num_cols(self) -> int:
        """Returns the number of columns."""

      def `CopyRowToVec` as copy_row_to_vec(self, row: int, v: VectorBase)

      def `CopyColToVec` as copy_col_to_vec(self, col: int, v: VectorBase)

      def `CopyToMat` as copy_to_mat_offset(self, row_offset: int,
                                         col_offset: int, dest: MatrixBase)

      def `Swap` as swap(self, other: CompressedMatrix):
        """Swap elements with another compressed matrix. Shallow swap.

        Args:
            other(CompressedMatrix): A compressed matrix

        Raises:
            Error in case od dimension mismatch.
        """

      def `Clear` as clear(self)

      def `Scale` as scale(self, alpha: float)
